class Merchant is subclass of User
types
	public ProductName = Product`ProductName;
	public NetworkBonus = Network`NetworkBonus;
values

instance variables
	bonus: NetworkBonus := 0;
	products: map ProductName to Product := {|->};
		
operations
	public Merchant: Merchant * UserName * UserBalance ==> Merchant
	Merchant(r,n,b) == (r.incrementReferrals(); name := n; balance := b; return self);
	
	public Merchant: UserName * UserBalance ==> Merchant
	Merchant(n,b) == (name := n; balance := b; return self);	
	
	public getProduct: ProductName ==> Product
	getProduct(n) == return products(n)
	pre n in set dom products;
	
	public addProduct: Product ==> ()
	addProduct(p) == products := products munion {p.getName() |-> p}
	pre p not in set rng products;
	
	public removeProduct: ProductName ==> Product
	removeProduct(n) == (
		dcl product: Product := getProduct(n);
		products := {n} <-: products;
		return product;
	);
	
	public getBonus: () ==> NetworkBonus
	getBonus() == return bonus;
	
	public incrementReferrals: () ==> ()
	incrementReferrals() == (
		dcl nr_referrals: nat := 10 * bonus / (1 - bonus);
		nr_referrals := nr_referrals + 1;
		bonus := nr_referrals / (nr_referrals + 10);
	);
	
functions

traces

end Merchant